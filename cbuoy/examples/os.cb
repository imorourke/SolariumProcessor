#include <kernel/kmalloc.cb>
#include <kernel/kserialio.cb>
#include <kernel/kdevice.cb>
#include <kernel/kfs.cb>
#include <kernel/ktsk.cb>
#include <kernel/kirq.cb>
#include <kernel/kclock.cb>
#include <kernel/krtc.cb>
#include <std/list.cb>
#include <std/string.cb>

const FD_SIZE_TABLE: u32 = 1024;
const FD_THREAD: u32 = 1;
struct fd_type_t {
    type: u32;
    val: *u32;
}
global FD_TABLE: [FD_SIZE_TABLE]fd_type_t;

const OS_INPUT_BUFFER_SIZE: u32 = 128;
const OS_ARG_SIZE: u32 = 128;

fn os_split_args(input: *u8, args: **u8) i32 {
    def count: i32 = 0;
    def c: *u8 = input;

    while (*c != '\0') {
        // Skip any leading spaces
        while ((*c == ' ') || (*c == '\n')) {
            c = c + 1;
        }

        // Add the argument if it is not the end
        if (*c == '\0') {
            break;
        }

        args[count] = c;
        count = count + 1;

        while ((*c != '\0') && (*c != ' ') && (*c != '\n')) {
            c = c + 1;
        }

        if (*c != '\0') {
            *c = '\0';
            c = c + 1;
        }
    }

    return count;
}

fn os_main() void {
    k_io_print("Starting C/Buoy OS\n");
    def input_buffer: [OS_INPUT_BUFFER_SIZE]u8;
    def arg_buffer: [OS_ARG_SIZE]*u8;
    def valid: u8 = 1;
    def disk: *k_fs_disk_t = 0;

    while (valid) {
        k_io_print("~> ");
        if (k_io_read_str_until(input_buffer, OS_INPUT_BUFFER_SIZE, '\n') > 0) {
            k_io_print(input_buffer);
            def count: i32 = os_split_args(input_buffer, &arg_buffer);

            k_io_print("Arg Count: ");
            k_io_print_uint(count);
            k_io_print("\n");

            def i: i32 = 0;
            while (i < count) {
                k_io_print("  [");
                k_io_print_uint(i);
                k_io_print("] = ");
                k_io_print(arg_buffer[i]);
                k_io_print("\n");
                i = i + 1;
            }

            if (count > 0) {
                def cmd: *u8 = arg_buffer[0];
                k_io_print("CMD: ");
                k_io_print(cmd);
                k_io_print("\n");

                if (strcmp(cmd, "ls") == 0) {
                    k_io_print("No files found\n");
                } else if (strcmp(cmd, "pwd") == 0) {
                    k_io_print("/root\n");
                } else if (strcmp(cmd, "reset") == 0) {
                    k_cpu_reset();
                } else if (strcmp(cmd, "date") == 0) {
                    def dt: k_datetime_t = k_rtc_current();
                    k_io_print_uint(dt.year);
                    k_io_print("-");
                    k_io_print_uint(dt.month);
                    k_io_print("-");
                    k_io_print_uint(dt.day + 1);
                    k_io_print(" ");
                    k_io_print_uint(dt.hour);
                    k_io_print(":");
                    k_io_print_uint(dt.minute);
                    k_io_print(":");
                    k_io_print_uint(dt.second);
                    k_io_print("\n");
                } else if (strcmp(cmd, "mount") == 0) {
                    if (disk == 0) {
                        disk = k_fs_disk_open(0);
                        k_io_print("Disk ");
                        k_io_print("Disk Mounted\n");
                        k_io_print("  ");
                        k_io_print_uint((disk->vol_hdr).sector_size : u32);
                        k_io_print(" # ");
                        k_io_print_uint((disk->vol_hdr).sector_count : u32);
                        k_io_print("\n");
                    } else {
                        k_io_print("Disk already mounted\n");
                    }
                } else if (strcmp(cmd, "umount") == 0) {
                    if (disk != 0) {
                        k_fs_disk_close(disk);
                        disk = 0;
                        k_io_print("Disk Unmounted\n");
                    } else {
                        k_io_print("Disk Not Mounted\n");
                    }
                } else {
                    k_io_print("^! Unknown Command: ");
                    k_io_print(cmd);
                    k_io_print("\n");
                }
            }
        }
    }
}

global K_OS_THREAD: [4096]u8;

fn main() void {
    k_tsk_init(os_main, K_OS_THREAD);

    k_irq_set(1, k_tsk_yield);
    k_clock_configure(1000, 1);

    k_tsk_main();
    k_cpu_reset();
}
