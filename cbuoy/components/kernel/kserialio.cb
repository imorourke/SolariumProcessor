#ifndef K_SERIAL_IO
#define K_SERIAL_IO

#include kconfig.cb

global K_SERIAL_IN_SIZE: const *u8 = K_LOC_DEVICE_START + 2;
global K_SERIAL_IN_LOC: const *u8 = K_LOC_DEVICE_START + 3;
global K_SERIAL_OUT_LOC: const *u8 = K_LOC_DEVICE_START + 5;

//struct k_serial_control_t {
//    device_id: u16;
//    input_size: u8;
//    input_data: *u8;
//    output_size: u8;
//    output_set: *u8,
//}
//
//global SERIAL_DEV: *k_serial_control_t = K_LOC_DEVICE_START;

#ifdef K_SERIAL_IO_CBUOY_PRINT
fn k_io_print(c: *u8) void {
    while (*c != 0) {
        *K_SERIAL_OUT_LOC = *c;
        c = c + 1;
    }
}

fn k_io_print_digit(v: u8) void {
    if (v < 10) {
        *K_SERIAL_OUT_LOC = '0' + v;
    } else {
        *K_SERIAL_OUT_LOC = '?';
    }
}
#else
asmfn k_io_print(c: *u8) void {
    "push $stat";
    "intoff";
    "%LDLOC% 8:u32";
    ".loadloc %{K_SERIAL_OUT_LOC}%";
    "ld 8:u32 8";
    ".align";
    "ldi 9:u16 1";
    "ld 10:u32 $arg";
    "ldi 11:u16 @{c}@";
    "add 10:u32 10 11";
    "ld 11:u8 10";
    "tz 11";
    "jmpri 16";
    "sav 8:u8 11";
    "add 10:u32 10 9";
    "jmpri -20";
    "popr $stat";
    "ret";
}

asmfn k_io_print_digit(v: u8) void {
    "ldi 9:u16 @{v}@";
    "add 9:u32 $arg 9";
    "ld 9:u8 9";
    "ldi 10:u16 10";
    "%LDLOC% 11:u32";
    ".loadloc %{K_SERIAL_OUT_LOC}%";
    "ld 11:u32 11";
    "tl 12:u8 9 10";
    "tz 12";
    "jmpri 16";
    "ldi 10:u16 '0'";
    "add 10:u8 9 10";
    "jmpri 8";
    "ldi 10:u16 0x3F";
    "sav 11:u8 10";
    "ret";
}
#endif

fn k_io_print_uint(v: u32) void {
    def chars: [12]u8;
    def cp: *u8 = chars;
    if (v == 0) {
        k_io_print_digit(0);
    } else {
        while (v != 0) {
            *cp = v % 10;
            v = v / 10;
            cp = cp + 1;
        }
        while (cp != chars) {
            cp = cp - 1;
            k_io_print_digit(*cp);
        }
    }
}

fn k_io_print_hex(v: u32) void {
    def chars: [12]u8;
    def cp: *u8 = chars;
    if (v == 0) {
        k_io_print_digit(0);
    } else {
        while (v != 0) {
            *cp = v % 16;
            v = v / 16;
            cp = cp + 1;
        }
        while (cp != chars) {
            cp = cp - 1;
            if (*cp < 10) {
                *K_SERIAL_OUT_LOC = '0' + *cp;
            } else {
                *K_SERIAL_OUT_LOC = 'A' + (*cp - 10);
            }
        }
    }
}

fn k_io_print_stat(name: *u8, v: u32) void {
    k_io_print(name);
    k_io_print(": ");
    k_io_print_uint(v);
    k_io_print("\n");
}

fn k_io_read_char() u8 {
    return *K_SERIAL_IN_LOC;
}

fn k_io_read_char_immediate() u8 {
    if (*K_SERIAL_IN_SIZE > 0) {
        return *K_SERIAL_IN_LOC;
    } else {
        return 0u8;
    }
}

// NOTE - WILL NULL TERMINATE!
fn k_io_read_str_until(buffer: *u8, len: u32, target: u8) u32 {
    if (len == 0) {
        return 0;
    } else {
        def i: *u8 = buffer;
        def end: *u8 = (buffer + len - 1);

        def num_read: u32 = 0;

        while (i != end) {
            if (*K_SERIAL_IN_SIZE > 0) {
                *i = *K_SERIAL_IN_LOC;
                num_read = num_read + 1;
                if (*i == target) {
                    *(i + 1) = 0;
                    break;
                }
                i = i + 1;
            }
        }

        return num_read;
    }
}

#endif // K_SERIAL_IO
